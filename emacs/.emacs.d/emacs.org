#+title: GNU Emacs configuration
#+author: Laurent Stacul
#+email: laurent.stacul@gmail.com
#+language: en

* My Emacs Configuration

** Default Theme

I like the modus theme that comes with GNU emacs. I start with the
light theme knowing I can easily change the the dark theme with the
command =M-x modus-themes-toggle=.

#+begin_src emacs-lisp :tangle "init.el"
  (load-theme 'modus-operandi t)
#+end_src

** Turn Off The Bell

I need silence when I work and the bell sound makes me nervous.

#+begin_src emacs-lisp :tangle "init.el"
  (setq ring-bell-function 'ignore)
  (setq visible-bell t)
#+end_src

** Remove All GUI Widgets

When I start emacs, I want it with the minimal view (no buttons, no
scroll bars, no tooltips) and I want the first show the Lisp scratch
buffer.

#+begin_src emacs-lisp :tangle "init.el"
  (setq inhibit-splash-screen t)
  (menu-bar-mode -1)
  (tool-bar-mode -1)
  (tooltip-mode -1)
  (if (display-graphic-p) (scroll-bar-mode -1))
  (setq use-dialog-box nil)
#+end_src

** Do not create lock and backup files

Most of the time, my files are in project managed with git. So, backup
files are generally useless. And regarding lock files, I generally
know what I do when using external programs.

#+begin_src emacs-lisp :tangle "init.el"
  (setq make-backup-files nil)
  (setq backup-inhibited nil)
  (setq create-lockfiles nil)
#+end_src

** Default Input Language

To swith to an other keyboard layout within Emacs, we use C-\
(toggle-input-method). By default, we are prompted to choose the input
method we want to use. As I generally switch away from English to
French, I set the following so that I don't have the select the wanted
layout. If I really want to choose another input method, I can use C-u
C-\.

#+begin_src emacs-lisp :tangle "init.el"
  (setq default-input-method "french-postfix")
#+end_src

** Display Configuration For MacBook

Display is hard because it depends upon the size of the monitor, its
definition, whether the user chose a scaled definition at OS level.

To simplify the logic of setting the right font size on each monitor,
I maintain a list of my monitors attributes. The =mm-size= is enough
to discriminate between each of the screens I use. So, I use this to
map to the desired font size.

#+begin_src emacs-lisp :tangle "init.el"
  (setq stac/monitors
        '(((mm-size 301 195) (font-size 120))
          ((mm-size 599 329) (font-size 160))))

  (defun stac/adapt-font-size (&optional frame)
    "Adapt the default font size depending on the monitor that FRAME is
  displayed on. If FRAME is nil, the monitor the current frame is
  displayed on is used to set the desired font size."
    (interactive)
    (let* ((current-monitor (frame-monitor-attributes frame))
           (size (alist-get 'mm-size current-monitor))
           selected-font-size)
      (dolist (my-monitor stac/monitors)
        (when (equal (alist-get 'mm-size my-monitor) size)
          (setq selected-font-size (car (alist-get 'font-size my-monitor)))))
      (unless selected-font-size
        (setq selected-font-size 100))
      (set-face-attribute 'default nil :height selected-font-size)))

  (stac/adapt-font-size)
#+end_src

** Package System Configuration

I add melpa and melpa-stable to the package repositories.

#+begin_src emacs-lisp :tangle "init.el"
  (require 'package)

  (add-to-list 'package-archives
               '("melpa" . "https://melpa.org/packages/") t)
  (add-to-list 'package-archives
               '("melpa-stable" . "https://stable.melpa.org/packages/") t)

  (setq package-archive-priorities
        '(("melpa-stable" . 1)
          ("gnu" . 1)
          ("nongnu" . 1)
          ("melpa" . 0)))

  (package-initialize)
#+end_src

In case, I install my configuration on a new machine, I update the
package index.

#+begin_src emacs-lisp :tangle "init.el"
  (unless package-archive-contents (package-refresh-contents))
#+end_src

In case some packages are missing, Emacs will propose me to install
them. On a new installation, all the packages will hence be installed.

#+begin_src emacs-lisp :tangle "init.el"
  (require 'use-package)
  (setq use-package-always-ensure t)
#+end_src

** Cursor Display Configuration

By default, in graphical mode the cursor blinks 10 times then remain
displayed. I prefer the cursor to blink forever.

#+begin_src emacs-lisp :tangle "init.el"
  ;; The cursor will blink forever
  (setq blink-cursor-blinks 0)
#+end_src

** Display Cursor Column Number

The minor mode =column-number-mode= will display the current column of
the cursor next to the line number in the mode line.

#+begin_src emacs-lisp :tangle "init.el"
  (setq column-number-mode t)
#+end_src

** Increase the Pulse Timer

When I use Xref to see the results of a search, I find the temporarily
hightlighted finding in the other buffer vanishes to quickly. The
default value is =0.03= and turning it to =0.08= is a good compromise.

#+begin_src emacs-lisp :tangle "init.el"
  (setq pulse-delay 0.08)
#+end_src

** Add Newline Character At End Of File

To avoid the terminal prompt to be moved right after the output a ~cat
file~ where =file= does not end with a newline character, it is a good
practice to impose your editor to always add that character on save
when needed.

#+begin_src emacs-lisp :tangle "init.el"
  (setq require-final-newline t)
#+end_src

** Show Trailing Whitespace

It is generally prettier to not have trailing white space in source
code. With the following setting, such trailing white spaces will be
visible. A simple way to remove them is to run the
=M-x delete-trailing-whitespace= command.

#+begin_src emacs-lisp :tangle "init.el"
  (add-hook 'prog-mode-hook (lambda() (setq show-trailing-whitespace t)))
  (add-hook 'text-mode-hook (lambda() (setq show-trailing-whitespace t)))
#+end_src

** Current Line Highlighting

When editing text or source code, we can have the current line
highlighted. But this is not useful in other buffers like the shell or
dired. The following only enable this mode when editing text and
source code.

There is a special =conf-mode= that is neither text or programming
mode. I enable this in all those derived modes.

#+begin_src emacs-lisp :tangle "init.el"
  (add-hook 'text-mode-hook #'hl-line-mode)
  (add-hook 'prog-mode-hook #'hl-line-mode)
  (add-hook 'conf-mode-hook #'hl-line-mode)
#+end_src

** Line Number

When programming, I need the line number to be displayed so that I
immediately see which line fails when something crash.

#+begin_src emacs-lisp :tangle "init.el"
  (add-hook 'prog-mode-hook 'display-line-numbers-mode)
#+end_src

** Never Indent With Tabs

I really dislike indentation with tabs. This will not change the
behavior when editing file that require indentation to be a TAB (like
=Makefile= by default).

#+begin_src emacs-lisp :tangle "init.el"
  (setq-default indent-tabs-mode nil)
#+end_src

** Stop using ~custom-file~

When we install a package via the package manager or we use =M-x
customize=, emacs writes things at the end of the emacs
configuration. We move such things in a temporary file because I don't
want to commit generated code.

#+begin_src emacs-lisp :tangle "init.el"
  (setq custom-file (make-temp-file "emacs-custom-"))
#+end_src

** History

To have the most recent file visited first, we can activate the
=savehist-mode=.

#+begin_src emacs-lisp :tangle "init.el"
  ;; Save history
  (use-package savehist
    :init
    (setq history-length 25)
    (savehist-mode))
#+end_src

We can even keep track of the position of the cursor in each visited
files with =save-place-mode=.

#+begin_src emacs-lisp :tangle "init.el"
  ;; Remember the last place in a visited file
  (use-package saveplace
    :init
    (save-place-mode))
#+end_src

** Automatic Buffer Update

By default, when a file is changed from outside of emacs, it is not
reflected inside the buffer: we need to manually use =M-x
revert-buffer=. But, this can be automated with the following.

#+begin_src emacs-lisp :tangle "init.el"
  ;; Refresh buffer when underlying file has changed
  (global-auto-revert-mode 1)
#+end_src

The same behaviour can be enabled for non-file buffers. For instance,
when we have a =Dired= buffer and we created a new file from outside,
the buffer does not display the created file. To automatically update
the buffer, we can set the following:

#+begin_src emacs-lisp :tangle "init.el"
  ;; Refresh other buffer if needed (for instance Dired buffers)
  (setq global-auto-revert-non-file-buffers t)
#+end_src

** GnuPG

Generally speaking, your GnuPG keys are protected with a
passphrase. To allow emacs to ask you directly to enter the key
passphrase, you must configure the =gpg-agent= to allow pinentry to be
used as a proxy instead of displaying one of its GUI.

So in your =.gnupg/gpg-agent.conf=, allow the =loopback= mode. At the
time of writing, I am not sure whether the option
=allow-emacs-pinentry= is required though.

#+begin_quote
allow-emacs-pinentry
allow-loopback-entry
#+end_quote

(If you change the configuration, don't forget to reload the
=gpg-agent= to take the changes into account with =gpgconf --reload
gpg-agent=.)

Then in Emacs:

#+begin_src emacs-lisp :tangle "init.el"
  ;; Prompt for passphrase in Emacs
  (setq epg-pinentry-mode 'loopback)
#+end_src

Now, when you =.authinfo.gpg= needs to be decrypted, Emacs will ask
you for the used key's passphrase.

** Display The Current Time In The Mode Line

#+begin_src emacs-lisp :tangle "init.el"
  (use-package time
    :ensure nil
    :hook (after-init . display-time-mode)
    :config
    (setq display-time-interval 60)
    (setq display-time-default-load-average nil))
#+end_src

** Display The Battery Status

When working on a laptop, it is generally a good idea to have the
battery status displayed in the mode line.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package battery
    :ensure nil
    :config
    (when (and battery-status-function
               (not (string-match-p "N/A"
                                    (battery-format "%B"
                                                    (funcall battery-status-function)))))
      (display-battery-mode 1)))
#+end_src

** Mode Line Configuration

#+begin_src emacs-lisp :tangle "init.el"
  (defun stac-mode-line-major-mode-name ()
    "Display the capitalized '-mode' truncated major mode."
    (capitalize (string-replace "-mode" "" (symbol-name major-mode))))

  (defvar stac-mode-line-major-mode
    '(:eval
      (concat "(" (stac-mode-line-major-mode-name) ")"))
    "My display of mode in the mode-line")

  (defvar stac-mode-line-misc-info
    '(:eval
      (when (mode-line-window-selected-p)
        mode-line-misc-info))
    "Only display misc info (like the current time) on the
  currently selected window.")

  (dolist (construct '(stac-mode-line-major-mode
                       stac-mode-line-misc-info))
    (put construct 'risky-local-variable t))

  (setq-default mode-line-format
        '("%e" mode-line-front-space
          (:propertize
           ("" mode-line-mule-info mode-line-client mode-line-modified mode-line-remote)
           display
           (min-width
            (5.0)))
          mode-line-frame-identification
          mode-line-buffer-identification
          "   "
          mode-line-position
          (vc-mode vc-mode)
          "  "
          stac-mode-line-major-mode
          " "

          stac-mode-line-misc-info
          mode-line-end-spaces))
#+end_src

** Ispell Configuration

Ispell is a wrapper around spell checking tools like Aspell, Ispell or
Hunspell. Thoses tools are external to Emacs so they have to be
configured independently and the dictionaries have to be installed
manually.

I use =Hunspell=. Dictionaries are available in
=git://anongit.freedesktop.org/libreoffice/dictionaries= GIT
repository.

On MacOS, I cloned this repository and created symbolic links:

#+begin_src bash
  ln -s ~/opensource/dictionaries/en/en_US.aff ~/opensource/dictionaries/en/en_US.dic ~/Library/Spelling/
#+end_src

** =tree-sitter=

=tree-sitter= is supported by Emacs since version 29. The only thing
to configure is the location of the grammar. To compile such grammar,
use the function =treesit-install-language-grammar=.

#+begin_src emacs-lisp :tangle "init.el"
  (setq treesit-language-source-alist
        '((bash "https://github.com/tree-sitter/tree-sitter-bash")
          (dockerfile "https://github.com/camdencheek/tree-sitter-dockerfile.git")
          (json "https://github.com/tree-sitter/tree-sitter-json")
          (yaml "https://github.com/tree-sitter-grammars/tree-sitter-yaml.git")
          (ruby "https://github.com/tree-sitter/tree-sitter-ruby.git")))
#+end_src

The following configuration overrides the default major mode in favor
their =tree-sitter= counterparts.

#+begin_src emacs-lisp :tangle "init.el"
  (setq major-mode-remap-alist
        '((shell-script-mode . bash-ts-mode)
          (json-mode . json-ts-mode)
          (yaml-mode . yaml-ts-mode)
          (ruby-mode . ruby-ts-mode)))
#+end_src

** Project

*** Switch Commands

Pressing =C-x p p= and selecting a project provides several
options. This part configures the proposed options:

- Removes the VC panel
- Add the option to switch to a buffer of the select project

#+begin_src emacs-lisp :tangle "init.el"
  (use-package project
    :config
    (assq-delete-all 'project-vc-dir project-switch-commands)
    (assq-delete-all 'project-eshell project-switch-commands)
    (add-to-list 'project-switch-commands '(project-switch-to-buffer "Buffer") t)
    (add-to-list 'project-switch-commands '(magit-project-status "Magit") t))
#+end_src

*** Project Tags Visiting

When a move from one project to another, I don't want to manually call
=M-x visit-tags-table=. The following snippet will do the job for me
by pressing =C-x p t=.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package project
    :init
    (defun stac/project-tags ()
      "When in a project, visit the tags file at the root of the project."
      (interactive)
      (if (project-current)
          (let* ((proj-root (expand-file-name (project-root (project-current))))
                 (old-tags-file tags-file-name)
                 (new-tags-file (concat proj-root "TAGS")))
            (if (equal old-tags-file new-tags-file)
                (message "Tags file not changed: %s" old-tags-file)
              (visit-tags-table new-tags-file)
              (message "Tags file changed: %s -> %s" old-tags-file new-tags-file)))
        (message "No current project")))
    :bind
    (:map project-prefix-map
          ("t" . stac/project-tags)
          ("m" . magit-project-status)))
#+end_src

** Ediff Configuration

When Emacs runs in graphical mode, starting a new =Ediff= session is
done in a new frame which I don't want. I changed the value of
=ediff-window-setup-function= to ='ediff-setup-windows-plain= which is
what happens when Emacs runs in a terminal.

By default, =Ediff= splits the window vertically but I am more used to
have an horizontal split, so I changed =ediff-split-window-function=
accordingly.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package ediff
    :ensure nil
    :config
    (setq ediff-split-window-function #'split-window-horizontally)
    (setq ediff-window-setup-function #'ediff-setup-windows-plain))
#+end_src

** Isearch Configuration

We display the number of matches and current match the cursor is on:
this will be displayed on the left of the =I-search= prompt.

When we search for a string, spaces are interpreted was catch-all so
that we have some kind of fuzzy search.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package isearch
    :ensure nil
    :demand t
    :config
    (setq search-whitespace-regexp ".*?"
          isearch-lazy-count t
          lazy-count-prefix-format "(%s/%s) "
          lazy-count-suffix-format nil))
#+end_src

** Xref

By default, =Xref= opens a new buffer when it can find several
definitions of the same symbol. With this configuration, I leverage
the minibuffer to make a choice.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package xref
    :ensure nil
    :commands (xref-find-definitions xref-go-back)
    :config
    (setq xref-show-definitions-function #'xref-show-definitions-completing-read))
#+end_src

** Speed Up Grep

The idea is to leverage =ripgrep= program if installed on the system.

=Xref= is already aware of the =rg= command to issue (see
=xref-search-program-alist=) so we simply have to set the variable
=xref-search-program= to the symbol ~'ripgrep~.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package grep
    :ensure nil
    :commands (grep lgrep rgrep)
    :config
    (let* ((executable (or (executable-find "rg") "grep"))
           (rgp (string-match-p "rg" executable)))
      (when rgp
        (setq grep-program executable)
        (setq grep-template "rg -nH --null -e <R> <F>")
        (setq xref-search-program 'ripgrep))))
#+end_src

** Dired Configuration

*** Move to Trash Instead of Removing a File

It is safer that removed files end up into the Trash bin than being
removed forever from the disk.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package dired
    :ensure nil
    :commands (dired)
    :config
    (setq delete-by-moving-to-trash t))
#+end_src

** Mails

*** =SMTP=

Let's always have a buffer named *trace of SMTP session to
<something>* which shows the data exchanges.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package smtpmail
    :ensure nil
    :config
    (setq smtpmail-debug-info t)
    (setq send-mail-function #'smtpmail-send-it))
#+end_src

*** =mu4e=

#+begin_src emacs-lisp :tangle "init.el"
  (use-package mu4e
    :ensure nil
    :if (locate-library "mu4e.el")
    :config
    (setq mu4e-confirm-quit nil)
    (setq mu4e-hide-index-messages t)
    (setq mu4e-change-filenames-when-moving t)
    (setq mu4e-update-interval (* 10 60))
    (setq mu4e-get-mail-command "mbsync -a")
    (setq mu4e-maildir "~/Mail")
    (setq mu4e-context-policy 'pick-first)
    (setq mu4e-headers-fields '((:human-date . 20)
                                (:flags . 6)
                                (:mailing-list . 10)
                                (:from . 22)
                                (:subject)))
    (setq mu4e-attachment-dir "~/Downloads")
    (setq stac/emacs-mailing-lists
          (list "help-gnu-emacs.gnu.org"
                "emacs-devel.gnu.org"
                "info-gnu-emacs.gnu.org"
                "bug-gnu-emacs.gnu.org"))
    (setq stac/development-mailing-lists
          (append stac/emacs-mailing-lists))
    (defun stac/mu4e-bookmark-mailing-list-query (mailing-lists)
      (format "(%s)"
              (mapconcat (lambda (s) (format "list:%s" s)) mailing-lists " or ")))
    (setq mu4e-bookmarks
          '(
            (
             :name "Unread messages"
             :query "flag:unread AND NOT flag:trashed AND NOT flag:list"
             :key ?u
             )
            (
             :name "From Emacs Lists"
             :query (lambda () (concat "flag:unread AND "
                                       "NOT flag:trashed AND "
                                       "flag:list AND "
                                       (stac/mu4e-bookmark-mailing-list-query stac/emacs-mailing-lists)))
             :key ?e
             )
            (
             :name "From Other Lists"
             :query (lambda () (concat "flag:unread AND "
                                       "NOT flag:trashed AND "
                                       "flag:list AND NOT "
                                       (stac/mu4e-bookmark-mailing-list-query stac/development-mailing-lists)))
             :key ?l
             )
            (
             :name "Today's messages"
             :query "date:today..now"
             :key ?t)
            )
          )

    (setq mu4e-contexts
          `(,(make-mu4e-context
              :name "laurent.stacul@gmail.com"
              :match-func
              (lambda (msg)
                (when msg
                  (string-prefix-p "/laurent.stacul@gmail.com" (mu4e-message-field msg :maildir))))
              :vars '((user-mail-address . "laurent.stacul@gmail.com")
                      (user-full-name    . "Laurent Stacul")
                      (smtpmail-smtp-server  . "smtp.gmail.com")
                      (smtpmail-smtp-service . 465)
                      (smtpmail-stream-type  . ssl)
                      (mu4e-drafts-folder  . "/laurent.stacul@gmail.com/[Gmail]/Brouillons")
                      (mu4e-sent-folder  . "/laurent.stacul@gmail.com/[Gmail]/Messages envoy&AOk-s")
                      (mu4e-refile-folder  . "/laurent.stacul@gmail.com/[Gmail]/Tous les messages")
                      (mu4e-trash-folder  . "/laurent.stacul@gmail.com/[Gmail]/Corbeille")
                      ;; don't save message to Sent Messages, Gmail/IMAP takes care of this
                      (mu4e-sent-messages-behavior . delete)
                      (mu4e-maildir-shortcuts . ((:maildir "/laurent.stacul@gmail.com/Inbox" :key ?i)))))
            ,(make-mu4e-context
              :name "captain.stac@gmail.com"
              :match-func
              (lambda (msg)
                (when msg
                  (string-prefix-p "/captain.stac@gmail.com" (mu4e-message-field msg :maildir))))
              :vars '((user-mail-address . "captain.stac@gmail.com")
                      (user-full-name    . "Laurent Stacul")
                      (smtpmail-smtp-server  . "smtp.gmail.com")
                      (smtpmail-smtp-service . 465)
                      (smtpmail-stream-type  . ssl)
                      (mu4e-drafts-folder  . "/captain.stac@gmail.com/[Gmail]/Brouillons")
                      (mu4e-sent-folder  . "/captain.stac@gmail.com/[Gmail]/Messages envoy&AOk-s")
                      (mu4e-refile-folder  . "/captain.stac@gmail.com/[Gmail]/Tous les messages")
                      (mu4e-trash-folder  . "/captain.stac@gmail.com/[Gmail]/Corbeille")
                      ;; don't save message to Sent Messages, Gmail/IMAP takes care of this
                      (mu4e-sent-messages-behavior . delete)
                      (mu4e-maildir-shortcuts . ((:maildir "/captain.stac@gmail.com/Inbox" :key ?i)))))
            ,(make-mu4e-context
              :name "La Poste"
              :match-func
              (lambda (msg)
                (when msg
                  (string-prefix-p "/laurent.stacul@laposte.net" (mu4e-message-field msg :maildir))))
              :vars '((user-mail-address . "laurent.stacul@laposte.net")
                      (user-full-name    . "Laurent Stacul")
                      (smtpmail-smtp-server  . "smtp.laposte.net")
                      (smtpmail-smtp-service . 587)
                      (smtpmail-stream-type  . ssl)
                      (mu4e-drafts-folder  . "/laurent.stacul@laposte.net/DRAFT")
                      (mu4e-sent-folder  . "/laurent.stacul@laposte.net/OUTBOX")
                      (mu4e-refile-folder  . "/laurent.stacul@laposte.net/Inbox")
                      (mu4e-trash-folder  . "/laurent.stacul@laposte.net/TRASH")
                      (mu4e-maildir-shortcuts . ((:maildir "/laurent.stacul@laposte.net/Inbox" :key ?i)))))
            ,(make-mu4e-context
              :name "Proton"
              :match-func
              (lambda (msg)
                (when msg
                  (string-prefix-p "/laurent.stacul@protonmail.com" (mu4e-message-field msg :maildir))))
              :vars '((user-mail-address . "laurent.stacul@protonmail.com")
                      (user-full-name    . "Laurent Stacul")
                      (smtpmail-smtp-server  . "localhost")
                      (smtpmail-smtp-service . 1025)
                      (smtpmail-stream-type  . starttls)
                      (mu4e-drafts-folder  . "/laurent.stacul@protonmail.com/Drafts")
                      (mu4e-sent-folder  . "/laurent.stacul@protonmail.com/Sent")
                      (mu4e-refile-folder  . "/laurent.stacul@protonmail.com/Archive")
                      (mu4e-trash-folder  . "/laurent.stacul@protonmail.com/Trash")
                      (mu4e-maildir-shortcuts . ((:maildir "/laurent.stacul@protonmail.com/Inbox" :key ?i))))))))
#+end_src

** Packages

*** Magit

This is a very good interface to git.

On MacOS, the git version provided by =Homebrew= seems slower
compared with the stock binary and, from time to times, some operation
are temporarily failing with messages like:

#+begin_quote
apply: Doing vfork: Permission denied
#+end_quote

Hence, there are two solutions:
- Use =git= provided by Xcode (uninstall with ~brew uninstall git~)
- Or, set the path to the =git= binary with =magit-git-executable= to
  =/usr/bin/git=

In any case, =Magit= is running faster with the default =git=.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package magit)
#+end_src

*** Minibuffer Improvements: Vertico & Marginalia

=Vertico= takes care of displaying choices in the minibuffer as a
vertical list.
=Marginalia= will append any lines displayed in =Vertico= with some
documentation.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package vertico
    :init
    (vertico-mode)
    (setq vertico-cycle t))

  (use-package marginalia
    :init
    (marginalia-mode))
#+end_src

*** Yasnippet

This package allows to insert some common snippets. For me, this is
only useful in programming modes.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package yasnippet
    :config
    (setq yas-snippet-dirs '("~/.emacs.d/snippets"))
    (yas-reload-all)
    :hook (prog-mode . yas-minor-mode))
#+end_src

There is a project that maintains all sorts of snippets:

#+begin_src emacs-lisp :tangle "init.el"
  (use-package yasnippet-snippets)
#+end_src

*** Flymake

#+begin_src emacs-lisp :tangle "init.el"
  (use-package flymake
    :hook (prog-mode . flymake-mode)
    :config
    (define-key flymake-mode-map (kbd "M-n") 'flymake-goto-next-error)
    (define-key flymake-mode-map (kbd "M-p") 'flymake-goto-prev-error))
#+end_src

#+begin_src emacs-lisp :tangle "init.el"
  (use-package flymake-collection
    :hook (after-init . flymake-collection-hook-setup))
#+end_src

*** Markdown

Nice display of Markdown files. When editing, as these files are
supposed to be readable from a basic editor, it is more readable to
make the line not too long: 80 characters is perfect.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package markdown-mode
    :hook
    ((markdown-mode . auto-fill-mode)
     (markdown-mode . (lambda() (set-fill-column 80)))))
#+end_src

*** Ruby

**** =ruby-mode=

#+begin_src emacs-lisp :tangle "init.el"
  (use-package ruby-mode
    :config
    (setq ruby-align-to-stmt-keywords t)
    (setq ruby-align-chained-calls nil)
    (setq ruby-method-params-indent nil)
    (setq ruby-block-indent nil)
    (setq ruby-method-call-indent nil))
#+end_src

**** =rbenv=

This package ensures we run the right ruby interpreter depending on
the project I am working on. For instance, if my project is defined to
work with =2.7.8=, I don't want =Flycheck= to try to run =rubocop=
which may not be installed with the system-wide ruby interpreter.

I don't need to see the current version in my modeline so I set
=rbenv-show-active-ruby-in-modeline= to =nil=.

#+begin_src emacs-lisp
  (use-package rbenv
    :config
    (setq rbenv-show-active-ruby-in-modeline nil)
    (global-rbenv-mode))
#+end_src

**** =chruby=

#+begin_src emacs-lisp :tangle "init.el"
  (use-package chruby)
#+end_src

**** IRB Buffer

When using ruby in =org-mode=, we need this package to run blocks with
=:session=. Moreover, it gives a nice =irb= console within emacs.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package inf-ruby)
#+end_src

**** Automatic generation of the ~end~ keyword

It is quite convenient that emacs generate the ~end~ keyword on new
class, function or block.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package ruby-end)
#+end_src

*** YAML

Facilities to edit YAML files.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package yaml-mode)
#+end_src

*** Org-mode

Disabling confirmation on code execution.
Activating =ruby= and =python= for evalution in =org-mode= notebooks.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package org
    :config
    (setq org-confirm-babel-evaluate nil)
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((emacs-lisp . t)
       (shell . t)
       (ruby . t)
       (python . t))))
#+end_src

*** In Buffer Completion with =corfu=

#+begin_src emacs-lisp :tangle "init.el"
  (use-package emacs
    :ensure nil
    :demand t
    :config
    (setq tab-always-indent 'complete)
    (setq tab-first-completion 'word-or-paren-or-punct)
    (setq-default tab-width 4
                  indent-tabs-mode nil))
#+end_src

#+begin_src emacs-lisp :tangle "init.el"
  (use-package dabbrev
    :ensure nil
    :config
    (setq dabbrev-backward-only nil)
    (setq dabbrev-case-distinction 'case-replace)
    (setq dabbrev-case-fold-search nil)
    (setq dabbrev-case-replace 'case-replace)
    (setq dabbrev-check-other-buffers t)
    (setq dabbrev-eliminate-newlines t)
    (setq dabbrev-upcase-means-case-search t)
    (setq dabbrev-ignored-buffer-modes
          '(archive-mode image-mode docview-mode pdf-view-mode)))
#+end_src

#+begin_src emacs-lisp :tangle "init.el"
  (use-package corfu
    :ensure t
    :hook (after-init . global-corfu-mode))
#+end_src

*** Entertainment

**** Listening to MP3 : =bongo=

#+begin_src emacs-lisp :tangle "init.el"
  (use-package bongo
    :config
    (setq bongo-default-directory "~/Music"))
#+end_src

**** Listening to Radio: =eradio=

#+begin_src emacs-lisp :tangle "init.el"
  (use-package eradio
    :config
    (setq eradio-channels
          '(("France Inter" . "http://direct.franceinter.fr/live/franceinter-midfi.mp3")
            ("France Info" . "http://direct.franceinfo.fr/live/franceinfo-midfi.mp3")
            ("France Culture" . "https://direct.franceculture.fr/live/franceculture-midfi.mp3")
            ("France Musique" . "https://direct.francemusique.fr/live/francemusique-midfi.mp3")
            ("FIP" . "http://direct.fipradio.fr/live/fip-midfi.mp3")
            ("Radio Classique" . "http://icepe6.infomaniak.ch/radioclassique-high.mp3"))))
#+end_src

*** Web Server

It may be useful to start a Web server from a local directory for
instance when we develop some HTML pages. To start serving files from
a directory, use =M-x httpd-serve-directory=.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package simple-httpd)
#+end_src

*** Terraform

#+begin_src emacs-lisp :tangle "init.el"
  (use-package terraform-mode)
#+end_src
